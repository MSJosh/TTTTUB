SigninLogs
| where TimeGenerated > ago(30d) and ResultType == 0 // Filter logs from the last 30 days where the result type is 0 (successful sign-ins)
| mv-expand AuthFields=parse_json(AuthenticationDetails) // Expand the AuthenticationDetails JSON array into multiple records
| extend AuthenticationMethod = tostring(AuthFields.authenticationMethod) // Extract the authentication method from the expanded records
| where AuthenticationMethod !in~ ("Password","Previously satisfied","") // Filter out records where the authentication method is "Password", "Previously satisfied", or empty
| summarize // Summarize the data by user, counting the total MFA attempts, phone MFA attempts, other MFA attempts, listing the methods used, and finding the most recent phone MFA attempt
    TotalMFACount = count(), 
    PhonetxtMFACount = countif(AuthenticationMethod =~ 'Text message' or AuthenticationMethod startswith "Phone call"), 
    MobileApp = countif(AuthenticationMethod =~ 'Mobile app'),
    WHfB = countif(AuthenticationMethod =~ 'Windows Hello for Business'),
    NonMSFTApp = countif(AuthenticationMethod =~ 'Companion mobile app notification'),
    Passkey = countif(AuthenticationMethod =~ 'passkey'),
    Phonepush = countif(AuthenticationMethod =~ 'Passwordless phone sign-in'),
    Oauth = countif(AuthenticationMethod =~ 'OATH verification code'),
    OtherMFACount = countif(AuthenticationMethod !~ 'Other'), 
    MethodsUsed = make_set(AuthenticationMethod), 
    MostRecentPhoneMFA = maxif(TimeGenerated, AuthenticationMethod =~ 'Text message' or AuthenticationMethod startswith "Phone call") 
    by UserPrincipalName
| where PhonetxtMFACount > 0 // Calculate the percentage of phone MFA attempts out of total MFA attempts
| extend PhoneMFAPercent = toreal(PhonetxtMFACount) / toreal(TotalMFACount) // Calculate the percentage of phone MFA attempts out of total MFA attempts
| order by PhonetxtMFACount desc // Sort by the largest amount of phone MFA attempts
